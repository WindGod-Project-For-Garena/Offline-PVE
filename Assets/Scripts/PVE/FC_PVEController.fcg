import "StdLibrary.fcc" as std
import "EditorGenLib.fcc" as gen
import "Player.fcc" as player
import "List.fcc" as list
import "./FC_PVEMonsterManager.fcg" as monsterManager
import "../Static/FC_Utilities.fcg" as Utilities
import "Convert.fcc" as Convert
import "Items.fcc" as Items
import "../CustomEvent.fcc" as CustomEvent
import "Math.fcc" as Math

static graph FC_PVEController {
    // 控制器状态
    IsGameActive bool = false
    CurrentLevel int = 1
    CurrentWave int = 0
    MaxLevels int = 10
    MaxWaves int = 4
    weaponMultiplier float = 1.0
    healthMultiplier float = 1.0
    skillMultiplier float = 1.0

    currPlayer entity<Player> = nil
    currentWeapon entity<Weapon> = nil
    
    // 游戏配置
    WaveTimeLimit int = 30000  // 每波次时间限制（ms）
    
    
    // 统计信息
    GameStartTime int = 0
    
    // 波次倒计时
    WaveStartTime int = 0  // 波次开始时间/ms
    WaveCountdownInterval int = 1000  // 倒计时更新间隔（毫秒）
    IsCountdownActive bool = false
    
    event OnAwake() {
        LogInfo("PVE控制器初始化")
        InitializeController()
    }
    
    func InitializeController() {
        // 初始化游戏状态
        IsGameActive = false
        CurrentLevel = 0  // 初始化为0，表示未开始
        CurrentWave = 0
        IsCountdownActive = false
        
        LogInfo("PVE控制器初始化完成")
    }

    event OnPlayerJoin(player entity<Player>) {
        currPlayer = player
    }

    event OnGameStartClick(level int, skillIndex int, ability1Level int, ability2Level int, ability3Level int) {
        weaponMultiplier = 1.0 * ability1Level + 1
        healthMultiplier = 1.0 * ability2Level + 1
        skillMultiplier = 1.0 * ability3Level + 1

        //删除武器
        Destroy(currentWeapon)
        //临时发放武器
        AddItem(currPlayer, ItemIDType.GoldMP5_III, 1, out var itemList)
        currentWeapon = itemList[0] as entity<Weapon>
        currentWeapon<Weapon>.IsConsumeAmmo = false
        currentWeapon<Weapon>.Damage = Math.Ceil(currentWeapon<Weapon>.Damage * weaponMultiplier)
        currPlayer<Player>.IsDropOnEliminated = false

        //设置血量
        currPlayer<Player>.MaxHP = Math.Ceil(200 * healthMultiplier)
        currPlayer<Player>.HP = currPlayer<Player>.MaxHP

        //设置技能Multiplier

       
        LogInfo("游戏开始 - 关卡: " + level + "，技能: " + skillIndex)
        StartPVEGame(level)
    }

    event ToServer_StartNextWave() {
        CurrentWave = CurrentWave + 1
        StartWave(CurrentWave)
    }

    event S_Debug_NextWave() {
        IsCountdownActive = false
    }

  
    
    // 开始PVE游戏（指定关卡）
    func StartPVEGame(level int) {
        if IsGameActive {
            LogWarning("游戏已在进行中")
            return
        }
        
        // 验证关卡范围
        if level < 1 || level > MaxLevels {
            LogWarning("无效的关卡: " + level + "，有效范围: 1-" + MaxLevels)
            return
        }
        
        LogInfo("开始PVE游戏 - 关卡: " + level)
        IsGameActive = true
        GameStartTime = globalEntity<Global>.GameTimeCount
        CurrentLevel = level
        CurrentWave = 0
        // 通知所有玩家
        Utilities.NotifyAllPlayers("PVE游戏开始！关卡: " + level, #00FF00FF, 5000)

        //显示升级技能

        
        // 通知怪物管理器开始关卡
        monsterManager.LevelInitialize(level)

        //通知客户端显示升级技能UI(这里写法玩家硬写的)
        DispatchEventToEntity(ToClient_DisplaySkillLevelUpUI,GetAllPlayers()[0], PlatformType.Client, List<object>{})
        
        
        // 开始第一波
        //StartWave(1)
    }
    

  
    // 开始指定波次
    func StartWave(wave int) {
        CurrentWave = wave
        WaveStartTime = globalEntity<Global>.GameTimeCount
        IsCountdownActive = true
        
        LogInfo("开始关卡 " + CurrentLevel + " 波次 " + wave + "，时间限制: " + WaveTimeLimit/1000 + "秒")
        Utilities.NotifyAllPlayers("波次 " + wave + " 开始！时间限制: " + WaveTimeLimit/1000 + "秒", #FFFF00FF, 2000)
        
        // 通知怪物管理器开始波次
        monsterManager.StartWave(wave)
        
        // 开始倒计时
        start WaveCountdownTimer()
    }
    
    // 波次倒计时器
    async func WaveCountdownTimer() {
        var waveTime int = globalEntity<Global>.GameTimeCount - WaveStartTime
        while IsCountdownActive && waveTime < WaveTimeLimit {
            waveTime = globalEntity<Global>.GameTimeCount - WaveStartTime
            var remainTime int = (WaveTimeLimit - waveTime)/1000
            //LogInfo("波次倒计时: " + waveTime + "ms")
            //LogInfo("波次倒计时: " + waveTime/1000 + "秒")
            // 每10秒或最后10秒显示倒计时
            if remainTime % 10 == 0 || remainTime <= 10 {
                Utilities.NotifyAllPlayers("波次 " + CurrentWave + " 剩余时间: " + remainTime + "秒", #FFFFFF, 1000)
            }
            WaitForMillisecond(WaveCountdownInterval)
        }

        // 倒计时结束，检查玩家存活状态
        if IsCountdownActive && MaxWaves > CurrentWave {
            IsCountdownActive = false
            monsterManager.ForceEndWave()
            DispatchEventToEntity(ToClient_DisplaySkillLevelUpUI,GetAllPlayers()[0], PlatformType.Client, List<object>{})
        }else{
            monsterManager.ForceEndWave()
            OnGameComplete()
        }
    }
    
   

    
    // 检查玩家存活状态
    func CheckPlayersAlive() bool {
        var allPlayers = GetAllPlayers()
        for i = 0, list.Length(allPlayers), 1 {
            var player = allPlayers[i]
            if player<Player>.HP > 0 {
                return true  // 至少有一个玩家存活
            }
        }
        return false  // 所有玩家都死亡
    }
    
   
    
    
    // 显示最终统计
    func ShowFinalStats() {
        var currTime int = globalEntity<Global>.GameTimeCount as int

        var totalTime int = (currTime - GameStartTime) / 1000
        var message = "=== 最终统计 ==="
        message = message + " 总用时: " + totalTime + "秒"
        message = message + " 完成关卡: " + CurrentLevel
        
        Utilities.NotifyAllPlayers(message, #FFD700, 10000)
    }
    
    // 获取游戏状态
    func GetGameStatus() string {
        var status = "=== PVE游戏状态 ==="
        var gameState = "未开始"
        if IsGameActive {
            gameState = "进行中"
        }
        status = status + " 游戏状态: " + gameState
        if CurrentLevel > 0 {
            status = status + " 当前关卡: " + CurrentLevel
            status = status + " 当前波次: " + CurrentWave
            if IsCountdownActive {
                status = status + " 剩余时间: " + (WaveTimeLimit - (globalEntity<Global>.GameTimeCount - WaveStartTime))/1000 + "秒"
            }
        } else {
            status = status + " 当前关卡: 未开始"
            status = status + " 当前波次: 未开始"
        }
        status = status + " 最大关卡数: " + MaxLevels
        status = status + " 波次时间限制: " + WaveTimeLimit + "秒"
        
        return status
    }
    
    
    
    // 事件处理
    
    // 游戏失败
    func OnGameOver() {
        LogInfo("PVE游戏失败！")
        Utilities.NotifyAllPlayers("游戏失败！", #FF0000FF, 5000)
        EndPVEGame()
    }
    
    // 游戏完成
    async func OnGameComplete() {
        LogInfo("PVE游戏完成！")
        Utilities.NotifyAllPlayers("恭喜！PVE游戏完成！", #FFD700FF, 10000)  
        //GetAllPlayers()[0]<Player>
        var localPlayer entity<Player> = GetAllPlayers()[0]
        localPlayer<Player>.Invincible = true
        // 显示最终统计
        ShowFinalStats()

        // 等待一段时间后结束游戏
        WaitForMillisecond(3000)
        EndPVEGame()
    }


     // 结束PVE游戏
     func EndPVEGame() {
        LogInfo("结束PVE游戏")
        IsGameActive = false
        IsCountdownActive = false
        
        // 清理所有怪物
        monsterManager.ClearAllMonsters()
        
        
        // 清理BOSS
        //bossHandler.ForceEndBossFight()
        
        Utilities.NotifyAllPlayers("PVE游戏结束", #FF0000FF, 3000)
        var localPlayer entity<Player> = GetAllPlayers()[0]
        localPlayer<Player>.Invincible = false

        DispatchEventToEntity(ToClient_GameComplete,GetAllPlayers()[0], PlatformType.Client, List<object>{})
    }

    
    
    
   
}
