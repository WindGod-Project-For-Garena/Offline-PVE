import "StdLibrary.fcc" as std
import "EditorGenLib.fcc" as gen
import "Player.fcc" as player
import "List.fcc" as list
import "./FC_PVEMonsterManager.fcg" as monsterManager
import "../Static/FC_Utilities.fcg" as Utilities
import "Convert.fcc" as Convert

static graph FC_PVEController {
    // 控制器状态
    IsGameActive bool = false
    CurrentLevel int = 1
    CurrentWave int = 1
    MaxLevels int = 3
    MaxWaves int = 4
    
    // 游戏配置
    WaveTimeLimit int = 30000  // 每波次时间限制（ms）
    
    
    // 统计信息
    GameStartTime int = 0
    
    // 波次倒计时
    WaveStartTime int = 0  // 波次开始时间/ms
    WaveCountdownInterval int = 1000  // 倒计时更新间隔（毫秒）
    IsCountdownActive bool = false
    
    event OnAwake() {
        LogInfo("PVE控制器初始化")
        InitializeController()
    }
    
    func InitializeController() {
        // 初始化游戏状态
        IsGameActive = false
        CurrentLevel = 0  // 初始化为0，表示未开始
        CurrentWave = 0
        IsCountdownActive = false
        
        LogInfo("PVE控制器初始化完成")
    }
    
    // 开始PVE游戏（指定关卡）
    func StartPVEGame(level int) {
        if IsGameActive {
            LogWarning("游戏已在进行中")
            return
        }
        
        // 验证关卡范围
        if level < 1 || level > MaxLevels {
            LogWarning("无效的关卡: " + level + "，有效范围: 1-" + MaxLevels)
            return
        }
        
        LogInfo("开始PVE游戏 - 关卡: " + level)
        IsGameActive = true
        GameStartTime = globalEntity<Global>.GameTimeCount
        CurrentLevel = level
        CurrentWave = 1
        // 通知所有玩家
        Utilities.NotifyAllPlayers("PVE游戏开始！关卡: " + level, #00FF00FF, 5000)
        
        // 通知怪物管理器开始关卡
        monsterManager.LevelInitialize(level)
        
        // 开始第一波
        StartWave(1)
    }
    
  
    // 开始指定波次
    func StartWave(wave int) {
        CurrentWave = wave
        WaveStartTime = globalEntity<Global>.GameTimeCount
        IsCountdownActive = true
        
        LogInfo("开始关卡 " + CurrentLevel + " 波次 " + wave + "，时间限制: " + WaveTimeLimit/1000 + "秒")
        Utilities.NotifyAllPlayers("波次 " + wave + " 开始！时间限制: " + WaveTimeLimit/1000 + "秒", #FFFF00FF, 2000)
        
        // 通知怪物管理器开始波次
        monsterManager.StartWave(wave)
        
        // 开始倒计时
        start WaveCountdownTimer()
    }
    
    // 波次倒计时器
    async func WaveCountdownTimer() {
        var waveTime int = globalEntity<Global>.GameTimeCount - WaveStartTime
        while IsCountdownActive && waveTime < WaveTimeLimit {
            waveTime = globalEntity<Global>.GameTimeCount - WaveStartTime
            var remainTime int = (WaveTimeLimit - waveTime)/1000
            //LogInfo("波次倒计时: " + waveTime + "ms")
            //LogInfo("波次倒计时: " + waveTime/1000 + "秒")
            // 每10秒或最后10秒显示倒计时
            if remainTime % 10 == 0 || remainTime <= 10 {
                Utilities.NotifyAllPlayers("波次 " + CurrentWave + " 剩余时间: " + remainTime + "秒", #FFFFFF, 1000)
            }
            WaitForMillisecond(WaveCountdownInterval)
        }

        // 倒计时结束，检查玩家存活状态
        if IsCountdownActive {
            start StartNextWave()
        }
    }
    
    // 波次时间到
    async func StartNextWave() {
        IsCountdownActive = false
        monsterManager.ForceEndWave()

        // 检查玩家存活状态
        if CheckPlayersAlive() {
            LogInfo("波次 " + CurrentWave + " 时间到，玩家存活，进入下一波")
            Utilities.NotifyAllPlayers("波次 " + CurrentWave + " 完成！玩家存活！", #00FF00FF, 3000)
            
            // 检查是否还有下一波
            if CurrentWave < MaxWaves {
                // 等待一段时间后开始下一波
                //WaitForMillisecond(WaveDelay)

                Utilities.NotifyAllPlayers("波次 " + CurrentWave + "在5秒后开始！", #FF0000FF, 5000)
                WaitForMillisecond(5000)
               
                StartWave( CurrentWave + 1)
                

            } else {
                // 关卡完成
                start OnGameComplete()
            }
        } else {
            LogInfo("波次 " + CurrentWave + " 时间到，玩家死亡，游戏结束")
            Utilities.NotifyAllPlayers("波次 " + CurrentWave + " 失败！玩家死亡！", #FF0000FF, 5000)
            
            OnGameOver()
        }
    }

    
    // 检查玩家存活状态
    func CheckPlayersAlive() bool {
        var allPlayers = GetAllPlayers()
        for i = 0, list.Length(allPlayers), 1 {
            var player = allPlayers[i]
            if player<Player>.HP > 0 {
                return true  // 至少有一个玩家存活
            }
        }
        return false  // 所有玩家都死亡
    }
    
   
    
    
    // 显示最终统计
    func ShowFinalStats() {
        var currTime int = globalEntity<Global>.GameTimeCount as int

        var totalTime int = (currTime - GameStartTime) / 1000
        var message = "=== 最终统计 ==="
        message = message + " 总用时: " + totalTime + "秒"
        message = message + " 完成关卡: " + CurrentLevel
        
        Utilities.NotifyAllPlayers(message, #FFD700, 10000)
    }
    
    // 获取游戏状态
    func GetGameStatus() string {
        var status = "=== PVE游戏状态 ==="
        var gameState = "未开始"
        if IsGameActive {
            gameState = "进行中"
        }
        status = status + " 游戏状态: " + gameState
        if CurrentLevel > 0 {
            status = status + " 当前关卡: " + CurrentLevel
            status = status + " 当前波次: " + CurrentWave
            if IsCountdownActive {
                status = status + " 剩余时间: " + (WaveTimeLimit - (globalEntity<Global>.GameTimeCount - WaveStartTime))/1000 + "秒"
            }
        } else {
            status = status + " 当前关卡: 未开始"
            status = status + " 当前波次: 未开始"
        }
        status = status + " 最大关卡数: " + MaxLevels
        status = status + " 波次时间限制: " + WaveTimeLimit + "秒"
        
        return status
    }
    
    
    
    // 事件处理
    
    // 游戏失败
    func OnGameOver() {
        LogInfo("PVE游戏失败！")
        Utilities.NotifyAllPlayers("游戏失败！", #FF0000FF, 5000)
        EndPVEGame()
    }
    
    // 游戏完成
    async func OnGameComplete() {
        LogInfo("PVE游戏完成！")
        Utilities.NotifyAllPlayers("恭喜！PVE游戏完成！", #FFD700FF, 10000)  
        
        // 显示最终统计
        ShowFinalStats()

        // 等待一段时间后结束游戏
        WaitForMillisecond(3000)
        EndPVEGame()
    }


     // 结束PVE游戏
     func EndPVEGame() {
        LogInfo("结束PVE游戏")
        IsGameActive = false
        IsCountdownActive = false
        
        // 清理所有怪物
        monsterManager.ClearAllMonsters()
        
        // 清理BOSS
        //bossHandler.ForceEndBossFight()
        
        Utilities.NotifyAllPlayers("PVE游戏结束", #FF0000FF, 3000)
    }

    
    
    
   
}
