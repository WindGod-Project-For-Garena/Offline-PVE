import "StdLibrary.fcc" as StdLib
import "EditorGenLib.fcc" as EditorLib
import "Player.fcc" as Player
import "Hud.fcc" as Hud
import "Math.fcc" as Math
import "Camera.fcc" as Camera
import "Playable.fcc" as Playable
import "Animation.fcc" as Animation
import "../PlayerJionGame/PlayerJionGame.fcg" as PlayerJionGame
import "../DeBug/Range.fcg" as Range
import "../DeBug/Damage.fcg" as Damage
import "../DeBug/cdlevelup.fcg" as cdlevelup
import "../DeBug/ChoiceSkill.fcg" as ChoiceSkill
import "../SuperFcc/TransferList.fcc" as TransferList
import "List.fcc" as List
import "../CustomEvent.fcc" as CustomEvent

[platform_client]
graph SkillUI {
    previewEffectEntity entity<Effect>
    effectEntity entity<Effect>
    ScreenLayer int = 0
    tween entity<Tween>
    RotTime int = 1000
    ReleasedEffectPosition Vector3
    JoyStickButton entity<UIWidget>
    FillAmountTween entity<Tween>
    ActiveMonstersParentNode entity<Entity> 
    

    event OnAwake() {
        ActiveMonstersParentNode = EResSceneIsleLand.ActiveMobs as entity<Entity>
    }

    // 特效颜色
    func  BoomColor(createdEntity entity<Effect>,value int){
        var color = #FF98D4FF
        if value == 1 {
            color = #FF98D4FF 
        } else if value == 2 {
            color = #98FFFFFF 
        } else if value == 3 {
            color = #98FF98FF
        } else if value == 4 {
            color = #98CDFFFF 
        } else if value == 5 {
            color = #FF9898FF 
        } else if value == 6 {
            color = #FFF098FF 
        }
        createdEntity<Effect>.Color = color
    }

    func StartRotTween(createdEntity entity<Effect>){
        if tween == nil{
            var rotstart = Vector3{0, 0, 0}
            var rotend = Vector3{0, 360, 0}     
            CreateTween(
                createdEntity,
                Transform.Rotation,
                rotstart,
                rotend,
                TweenStyle.Loop,
                CreateCurve({0, 0, 1, 1}),
                RotTime,
                out var RotTweenEntity,
                true)
            tween = RotTweenEntity
        }
    }
    
    //————————————————————————————————————————————————//
    event OnJoyStickMove(player entity<Player>, delta Vector2){
        var TouchPoint Vector2 = Vector2{0,0}
        ScreenTouchRay(TouchPoint, out var startPosition, out var direction)
        var layerMask List<int> = List<int>{ScreenLayer}
        ScreenSingleRaycast(TouchPoint, 100, layerMask, true, out var hitEntity, out var hitPosition, out var hitDistance, out var hitNormal)
        ReleasedEffectPosition = hitPosition
        
        var CurrentSkill = GetLocalPlayer()<PlayerJionGame>.CurrentSkill
        if CurrentSkill == 1 {     
            Skill_1(CurrentSkill, hitPosition)
        }
        if CurrentSkill == 2 {
            Skill_2(CurrentSkill, hitPosition)
        }
    }

    // 技能1预览
    func Skill_1(CurrentSkill int, hitPosition Vector3){
        if previewEffectEntity == nil {
            CreateEffectClient(out var NewcreatedEntity, VFXType.LightBeamPreDiscarded, LoopType.Once, true)
            previewEffectEntity = NewcreatedEntity
        }
        
        var skillLevel = GetLocalPlayer()<PlayerJionGame>.GetCurrentSkillLevel(CurrentSkill)
        var skillDataInt = GetLocalPlayer()<PlayerJionGame>.GetSkillDataInt("Skill1", skillLevel)
        var Size = skillDataInt["size"]
        
        previewEffectEntity<Transform>.Scale = Vector3{Size, 0.1, Size}
        BoomColor(previewEffectEntity, Size)
        previewEffectEntity<Transform>.Position = hitPosition
        StartRotTween(previewEffectEntity)
    }

    // 技能2预览
    func Skill_2(CurrentSkill int, hitPosition Vector3){
        if previewEffectEntity == nil {
            CreateEffectClient(out var NewcreatedEntity, VFXType.LightBeamPreDiscarded, LoopType.Once, true)
            previewEffectEntity = NewcreatedEntity
        }
        
        var skillLevel = GetLocalPlayer()<PlayerJionGame>.GetCurrentSkillLevel(CurrentSkill)
        var skillDataInt = GetLocalPlayer()<PlayerJionGame>.GetSkillDataInt("Skill2", skillLevel)
        var Size = skillDataInt["Count"]
        
        previewEffectEntity<Transform>.Scale = Vector3{Size, 0.1, Size}
        BoomColor(previewEffectEntity, Size)
        previewEffectEntity<Transform>.Position = hitPosition
        StartRotTween(previewEffectEntity)
    }

    //————————————————————————————————————————————————//
    event OnJoyStickReleased(player entity<Player>){
        Destroy(previewEffectEntity)
        Destroy(tween)
        
        var CurrentSkill = GetLocalPlayer()<PlayerJionGame>.CurrentSkill
        if CurrentSkill == 1 {     
            start Skill_1_Released(CurrentSkill)
        }
        if CurrentSkill == 2 {
            start Skill_2_Released(CurrentSkill)
        }
    }

    // 技能1释放
    async func Skill_1_Released(CurrentSkill int){
        var skillLevel = GetLocalPlayer()<PlayerJionGame>.GetCurrentSkillLevel(CurrentSkill)
        var skillDataFloat = GetLocalPlayer()<PlayerJionGame>.GetSkillDataFloat("Skill1", skillLevel)
        var skillDataInt = GetLocalPlayer()<PlayerJionGame>.GetSkillDataInt("Skill1", skillLevel)
        
        var Size = skillDataInt["size"]
        var CurrentDamage = skillDataFloat["Damage"]
        var CurrentcdTime = skillDataInt["CD"]
        
        // 释放特效
        CreateEffectClient(out var NewcreatedEntity, VFXType.CurseExplosion, LoopType.Once, true)
        effectEntity = NewcreatedEntity
        effectEntity<Transform>.Position = ReleasedEffectPosition
        effectEntity<Transform>.Scale = Vector3{Size, Size, Size}
        BoomColor(effectEntity, Size)
       
        // 造成伤害
        var monsters = getMonsterWithinDistance(Size)
        for i = 0, List.Length(monsters), 1 {
            var monster = monsters[i]
            DispatchEventToEntity(OnDamageMob, monster, PlatformType.Server, List<object>{monster, nil, CurrentDamage, GetLocalPlayer()})
            CreateDamageNum(CurrentDamage, 10, #dde511, 800, monster as entity<Entity>, Vector3{0,1,0}, Vector2{60, 60}, true)
        }

        // 设置CD
        StartSkillCD(CurrentSkill, CurrentcdTime)
    }
    
    // 技能2释放
    async func Skill_2_Released(CurrentSkill int){
        var skillLevel = GetLocalPlayer()<PlayerJionGame>.GetCurrentSkillLevel(CurrentSkill)
        var skillDataFloat = GetLocalPlayer()<PlayerJionGame>.GetSkillDataFloat("Skill2", skillLevel)
        var skillDataInt = GetLocalPlayer()<PlayerJionGame>.GetSkillDataInt("Skill2", skillLevel)
        
        var Count = skillDataInt["Count"]
        var CurrentDamage = skillDataFloat["Damage"]
        var CurrentcdTime = skillDataInt["CD"]
        
        // 设置CD
        StartSkillCD(CurrentSkill, CurrentcdTime)
        
        // 释放雷击
        start PlayThunderEffects(Count, skillLevel, CurrentDamage)
    }

    // 启动技能CD
    func StartSkillCD(skillNum int, cdTime int) {
        var SkillUI entity<CustomUI> = GetLocalPlayer()<PlayerJionGame>.SkillUI
        var buttonPath = "/Skill_" + skillNum + "/JoyStickFillAmount"
        JoyStickButton = GetWidgetByPath(GetLocalPlayer(), SkillUI, buttonPath)
        JoyStickButton<UIWidget>.Active = true
        JoyStickButton<UIWidgetButton>.FillAmount = 0
        
        if FillAmountTween == nil{
            CreateTween(
                JoyStickButton,
                UIWidgetButton.FillAmount,
                0,
                1,
                TweenStyle.Once,
                CreateCurve({0, 0, 1, 1}),
                cdTime, 
                out var FillAmountTweenEntity,
                true)
            FillAmountTween = FillAmountTweenEntity
            start HandleSkillCD(cdTime)
        }
    }

    // 处理技能CD
    async func HandleSkillCD(cdTime int) {
        WaitForMillisecond(cdTime)
        Destroy(FillAmountTween)
        JoyStickButton<UIWidget>.Active = false
    }

    // 播放雷击特效
    async func PlayThunderEffects(Count int, skillLevel int, damage float) {
        for i = 0, Count, 1 {
            var randomMonster = GetRandomMonster()
            if randomMonster != nil {
                start CreateThunder(i, randomMonster, skillLevel, damage)
                WaitForMillisecond(200)
            }
        }
    }

    // 获取随机怪物
    func GetRandomMonster() entity<LevelObject> {
        var activeMonsters = GetChildren(ActiveMonstersParentNode)
        if List.Length(activeMonsters) == 0 { return nil }
        var randomIndex = RandomInt(0, List.Length(activeMonsters) - 1)
        return activeMonsters[randomIndex] as entity<LevelObject>
    }

    // 创建单道雷
    async func CreateThunder(index int, targetMonster entity<LevelObject>, skillLevel int, damage float) {
        var thunderPosition = targetMonster<Transform>.Position
        CreateEffectClient(out var thunderEffect, VFXType.Thunder, LoopType.Once, true)
        thunderEffect<Transform>.Position = thunderPosition
        thunderEffect<Transform>.Scale = Vector3{0.8,2,0.8}
        SetParentClient(targetMonster, thunderEffect, true)
        BoomColor(thunderEffect, skillLevel)
        ApplyThunderDamage(targetMonster, damage)
    }

    // 应用雷击伤害
    func ApplyThunderDamage(targetMonster entity<LevelObject>, damage float) {
        DispatchEventToEntity(OnDamageMob, targetMonster, PlatformType.Server, List<object>{targetMonster, nil, damage, GetLocalPlayer()})
        CreateDamageNum(damage, 10, #dde511, 800, targetMonster as entity<Entity>, Vector3{0,1,0}, Vector2{60, 60}, true)
    }

    // 获取范围内怪物
    func getMonsterWithinDistance(distance int) List<entity<LevelObject>> {
        var monsters List<entity<LevelObject>> = List<entity<LevelObject>>{}
        var activeMonsters List<entity<LevelObject>> = GetChildren(ActiveMonstersParentNode) as List<entity<LevelObject>>
        for i = 0, List.Length(activeMonsters), 1 {
            var monster = activeMonsters[i]
            if Distance(monster<Transform>.Position, ReleasedEffectPosition) <= distance {
                List.Append(monsters, monster)
            }
        }
        return monsters
    }
}