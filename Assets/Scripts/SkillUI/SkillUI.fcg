import "StdLibrary.fcc" as StdLib
import "EditorGenLib.fcc" as EditorLib
import "Player.fcc" as Player
import "Hud.fcc" as Hud
import "Math.fcc" as Math
import "Camera.fcc" as Camera
import "Playable.fcc" as Playable
import "Animation.fcc" as Animation
import "../PlayerJionGame/PlayerJionGame.fcg" as PlayerJionGame
import "../DeBug/Range.fcg" as Range
import "../DeBug/Damage.fcg" as Damage
import "../DeBug/cdlevelup.fcg" as cdlevelup
import "../DeBug/ChoiceSkill.fcg" as ChoiceSkill
import "../SuperFcc/TransferList.fcc" as TransferList
import "List.fcc" as List
import "../CustomEvent.fcc" as CustomEvent

[platform_client]
graph SkillUI {
    previewEffectEntity entity<Effect>
    effectEntity entity<Effect>
    ScreenLayer int = 0
    tween entity<Tween>
    RotTime int = 1000
    ReleasedEffectPosition Vector3
    JoyStickButton entity<UIWidget>
    FillAmountTween entity<Tween>
    ActiveMonstersParentNode entity<Entity> 

    skillMultiplier float = 1
    

    event OnAwake() {
        ActiveMonstersParentNode = EResSceneIsleLand.ActiveMobs as entity<Entity>
    }

    // 特效颜色
    func  BoomColor(createdEntity entity<Effect>,value int){
        var color = #FF98D4FF
        if value == 1 {
            color = #FF98D4FF 
        } else if value == 2 {
            color = #98FFFFFF 
        } else if value == 3 {
            color = #98FF98FF
        } else if value == 4 {
            color = #98CDFFFF 
        } else if value == 5 {
            color = #FF9898FF 
        } else if value == 6 {
            color = #FFF098FF 
        }
        createdEntity<Effect>.Color = color
    }

    func StartRotTween(createdEntity entity<Effect>){
        if tween == nil{
            var rotstart = Vector3{0, 0, 0}
            var rotend = Vector3{0, 360, 0}     
            CreateTween(
                createdEntity,
                Transform.Rotation,
                rotstart,
                rotend,
                TweenStyle.Loop,
                CreateCurve({0, 0, 1, 1}),
                RotTime,
                out var RotTweenEntity,
                true)
            tween = RotTweenEntity
        }
    }
    
    //————————————————————————————————————————————————//
    event OnJoyStickMove(player entity<Player>, delta Vector2){
        var TouchPoint Vector2 = Vector2{0,0}
        ScreenTouchRay(TouchPoint, out var startPosition, out var direction)
        var layerMask List<int> = List<int>{ScreenLayer}
        ScreenSingleRaycast(TouchPoint, 100, layerMask, true, out var hitEntity, out var hitPosition, out var hitDistance, out var hitNormal)
        ReleasedEffectPosition = hitPosition
        
        var CurrentSkill = GetLocalPlayer()<PlayerJionGame>.CurrentSkill
        if CurrentSkill == 1 {     
            Skill_1(CurrentSkill, hitPosition)
        }
        if CurrentSkill == 2 {
            Skill_2(CurrentSkill, hitPosition)
        }
    }

    // 技能1
    func Skill_1(CurrentSkill int, hitPosition Vector3){
        if previewEffectEntity == nil {
            CreateEffectClient(out var NewcreatedEntity, VFXType.LightBeamPreDiscarded, LoopType.Once, true)
            previewEffectEntity = NewcreatedEntity
        }
        
        // 获取技能1的范围等级
        var rangeLevel = GetLocalPlayer()<PlayerJionGame>.GetCurrentSkillLevel(CurrentSkill, "Range")
        var skillData = GetLocalPlayer()<PlayerJionGame>.GetSkillData("Skill1", rangeLevel)
        var Size = skillData["size"] as int
        
        previewEffectEntity<Transform>.Scale = Vector3{Size, 0.1, Size}
        BoomColor(previewEffectEntity, Size)
        previewEffectEntity<Transform>.Position = hitPosition
        StartRotTween(previewEffectEntity)
    }

    // 技能2
    func Skill_2(CurrentSkill int, hitPosition Vector3){
        if previewEffectEntity == nil {
            CreateEffectClient(out var NewcreatedEntity, VFXType.LightBeamPreDiscarded, LoopType.Once, true)
            previewEffectEntity = NewcreatedEntity
        }
        
        // 获取技能1的范围等级
        var rangeLevel = GetLocalPlayer()<PlayerJionGame>.GetCurrentSkillLevel(CurrentSkill, "Count")
        var skillData = GetLocalPlayer()<PlayerJionGame>.GetSkillData("Skill2", rangeLevel)
        var Size = skillData["Count"] as int
        
        previewEffectEntity<Transform>.Scale = Vector3{Size, 0.1, Size}
        BoomColor(previewEffectEntity, Size)
        previewEffectEntity<Transform>.Position = hitPosition
        StartRotTween(previewEffectEntity)
    }

    //————————————————————————————————————————————————//
    event OnJoyStickReleased(player entity<Player>){
        Destroy(previewEffectEntity)
        Destroy(tween)
        
        var CurrentSkill = GetLocalPlayer()<PlayerJionGame>.CurrentSkill
        if CurrentSkill == 1 {     
            start Skill_1_Released(CurrentSkill)
        }
        if CurrentSkill == 2 {
            start Skill_2_Released(CurrentSkill)
        }
    }

    // 技能1释放
    async func Skill_1_Released(CurrentSkill int){
        // 获取技能1的各个属性等级
        var rangeLevel = GetLocalPlayer()<PlayerJionGame>.GetCurrentSkillLevel(CurrentSkill, "Range")
        var damageLevel = GetLocalPlayer()<PlayerJionGame>.GetCurrentSkillLevel(CurrentSkill, "Damage")
        var cdLevel = GetLocalPlayer()<PlayerJionGame>.GetCurrentSkillLevel(CurrentSkill, "CD")
        
        var rangeData = GetLocalPlayer()<PlayerJionGame>.GetSkillData("Skill1", rangeLevel)
        var damageData = GetLocalPlayer()<PlayerJionGame>.GetSkillData("Skill1", damageLevel)
        var cdData = GetLocalPlayer()<PlayerJionGame>.GetSkillData("Skill1", cdLevel)
        
        var Size = rangeData["size"] as int
        var CurrentDamage = damageData["Damage"] as int
        var CurrentcdTime = cdData["CD"] as int
        
        // 释放特效
        CreateEffectClient(out var NewcreatedEntity, VFXType.CurseExplosion, LoopType.Once, true)
        effectEntity = NewcreatedEntity
        effectEntity<Transform>.Position = ReleasedEffectPosition
        effectEntity<Transform>.Scale = Vector3{Size, Size, Size}
        BoomColor(effectEntity, Size)
       
        // 造成伤害
        var monsters = getMonsterWithinDistance(Size)
        for i = 0, List.Length(monsters), 1 {
            var monster = monsters[i]
            var damage int = Math.Ceil(CurrentDamage * skillMultiplier)
            DispatchEventToEntity(OnDamageMob, monster, PlatformType.Server, List<object>{monster, nil, damage, GetLocalPlayer()})
            CreateDamageNum(damage, 10, #dde511, 800, monster as entity<Entity>, Vector3{0,1,0}, Vector2{60, 60}, true)
        }

        // 设置CD
        StartSkillCD(CurrentSkill, CurrentcdTime)
    }
    
    // 技能2释放
    async func Skill_2_Released(CurrentSkill int){
        // 获取技能2的各个属性等级
        var CountLevel = GetLocalPlayer()<PlayerJionGame>.GetCurrentSkillLevel(CurrentSkill, "Range")
        var damageLevel = GetLocalPlayer()<PlayerJionGame>.GetCurrentSkillLevel(CurrentSkill, "Damage")
        var cdLevel = GetLocalPlayer()<PlayerJionGame>.GetCurrentSkillLevel(CurrentSkill, "CD")
        
        var CountData = GetLocalPlayer()<PlayerJionGame>.GetSkillData("Skill2", CountLevel)
        var damageData = GetLocalPlayer()<PlayerJionGame>.GetSkillData("Skill2", damageLevel)
        var cdData = GetLocalPlayer()<PlayerJionGame>.GetSkillData("Skill2", cdLevel)
        
        var Count = CountData["Count"] as int
        var CurrentDamage = damageData["Damage"] as int
        var CurrentcdTime = cdData["CD"] as int

        // 设置CD
        StartSkillCD(CurrentSkill, CurrentcdTime)
        
        // 释放雷击
        start PlayThunderEffects(Count, CountLevel, CurrentDamage)
    }

    // 启动技能CD
    func StartSkillCD(skillNum int, cdTime int) {
        var SkillUI entity<CustomUI> = GetLocalPlayer()<PlayerJionGame>.SkillUI
        var buttonPath = "/Skill_" + skillNum + "/JoyStickFillAmount"
        JoyStickButton = GetWidgetByPath(GetLocalPlayer(), SkillUI, buttonPath)
        JoyStickButton<UIWidget>.Active = true
        JoyStickButton<UIWidgetButton>.FillAmount = 0
        
        if FillAmountTween == nil{
            CreateTween(
                JoyStickButton,
                UIWidgetButton.FillAmount,
                0,
                1,
                TweenStyle.Once,
                CreateCurve({0, 0, 1, 1}),
                cdTime, 
                out var FillAmountTweenEntity,
                true)
            FillAmountTween = FillAmountTweenEntity
            start HandleSkillCD(cdTime)
        }
    }

    // 处理技能CD
    async func HandleSkillCD(cdTime int) {
        WaitForMillisecond(cdTime)
        Destroy(FillAmountTween)
        JoyStickButton<UIWidget>.Active = false
    }

    // 播放雷击特效
    async func PlayThunderEffects(Count int, CountLevel int, damage int) {
        for i = 0, Count, 1 {
            var randomMonster = GetRandomMonster()
            if randomMonster != nil {
                start CreateThunder(i, randomMonster, CountLevel, damage)
                WaitForMillisecond(200)
            }
        }
    }

    // 获取随机怪物
    func GetRandomMonster() entity<LevelObject> {
        var activeMonsters = GetChildren(ActiveMonstersParentNode)
        if List.Length(activeMonsters) == 0 { return nil }
        var randomIndex = RandomInt(0, List.Length(activeMonsters) - 1)
        return activeMonsters[randomIndex] as entity<LevelObject>
    }

    // 创建单道雷
    async func CreateThunder(index int, targetMonster entity<LevelObject>, CountLevel int, damage int) {
        var thunderPosition = targetMonster<Transform>.Position
        CreateEffectClient(out var thunderEffect, VFXType.Thunder, LoopType.Once, true)
        thunderEffect<Transform>.Position = thunderPosition
        thunderEffect<Transform>.Scale = Vector3{0.8,2,0.8}
        SetParentClient(targetMonster, thunderEffect, true)
        BoomColor(thunderEffect, CountLevel)

        var realDamage int = Math.Ceil(damage * skillMultiplier)
        //造成伤害
        DispatchEventToEntity(OnDamageMob, targetMonster, PlatformType.Server, List<object>{targetMonster, nil, realDamage, GetLocalPlayer()})
        CreateDamageNum(realDamage, 10, #dde511, 800, targetMonster as entity<Entity>, Vector3{0,1,0}, Vector2{60, 60}, true)
      
    }


    // 获取范围内怪物
    func getMonsterWithinDistance(distance int) List<entity<LevelObject>> {
        var monsters List<entity<LevelObject>> = List<entity<LevelObject>>{}
        var activeMonsters List<entity<LevelObject>> = GetChildren(ActiveMonstersParentNode) as List<entity<LevelObject>>
        for i = 0, List.Length(activeMonsters), 1 {
            var monster = activeMonsters[i]
            if Distance(monster<Transform>.Position, ReleasedEffectPosition) <= distance {
                List.Append(monsters, monster)
            }
        }
        return monsters
    }

    
}